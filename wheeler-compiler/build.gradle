project(':wheeler-compiler') {
    configurations {
        antlrGenerator
    }

    dependencies {
        antlr 'org.antlr:antlr4:4.13.1'
        implementation project(':wheeler-core')
    }

    generateGrammarSource {
        maxHeapSize = "64m"
        arguments += ['-visitor',
            '-package', 'com.typeobject.wheeler.compiler.antlr',
            '-lib', 'src/main/antlr/com/typeobject/wheeler/compiler/antlr']
    }

    sourceSets {
        main {
            java {
                srcDirs += [
                    "${buildDir}/generated-src/antlr/main"
                ]
            }
        }
    }

    // Ensure ANTLR files are generated before compilation
    compileJava.dependsOn generateGrammarSource

    // Exclude generated ANTLR files from code quality checks
    tasks.withType(JavaCompile).configureEach {
        options.errorprone.excludedPaths = '.*/generated-src/.*'
    }

    spotless {
        java {
            targetExclude 'build/**', '**/generated-src/**'
        }
    }

    checkerFramework {
        extraJavacArgs += [
            '-AskipDefs=^com\\.typeobject\\.wheeler\\.compiler\\.antlr\\.'
        ]
    }
}
