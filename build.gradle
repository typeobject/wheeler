plugins {
    id 'net.ltgt.errorprone' version '3.1.0' apply false
    id 'com.diffplug.spotless' version '6.25.0' apply false
    id 'org.checkerframework' version '0.6.37' apply false
}

allprojects {
    group = 'com.typeobject.wheeler'
    version = '0.1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'antlr'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'org.checkerframework'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        annotationProcessor 'org.checkerframework:checker:3.42.0'
        errorprone 'com.google.errorprone:error_prone_core:2.24.1'
        
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.errorprone {
            disableWarningsInGeneratedCode = true
        }
    }

    spotless {
        java {
            targetExclude 'build/**'
            importOrder()
            removeUnusedImports()
            googleJavaFormat()
        }
    }

    checkerFramework {
        checkers = [
            'org.checkerframework.checker.nullness.NullnessChecker'
        ]
    }

    test {
        useJUnitPlatform()
    }
}
